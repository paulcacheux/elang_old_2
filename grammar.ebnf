program = function_def*
        ;

type = IDENTIFIER
     | '*' type
     | "()" # unit type
     ;

function_def = "fn" IDENTIFIER '(' param_list ')' [ "->" type ] block
             ;

param_list = [ param { ',' param } ]
           ;

param = IDENTIFIER ':' type
      ;

block = '{' statement* '}'
      ;

statement = let-statement
          | if-statement
          | loop-statement
          | while-statement
          | break-statement
          | continue-statement
          | return-statement
          | expr-statement
          | block
          ;

if-statement = "if" expression statement [ "else" statement ]
             ;

loop-statement = "loop" statement
               ;

while-statement = "while" expression statement
                ;

break-statement = "break" ";"
                ;

continue-statement = "continue" ";"
                   ;

return-statement = "return" [ expression ] ";"
                 ;

let-statement = "let" IDENTIFIER [ ':' type ] "=" expression ";"
              ;

expr-statement = expression ";"
               ;

expression = IDENTIFIER '=' expression
           | logical_or
           ;

logical_or = logical_and { "||" logical_and }
           ;

logical_and = eq_comp { "&&" eq_comp }
            ;

eq_comp = ord_comp [ (==|!=) ord_comp ]
        ;

ord_comp = sum [ (<|<=|>|>=) sum ]
         ;

sum = factor { (+|-) factor }
    ;

factor = cast { (*|/|%) cast }
       ;

cast = term [ "as" type ]
     ;

term = IDENTIFIER
     | IDENTIFIER '(' expr_list ')'
     | LITERAL
     | [+|-|!] term
     | '(' expression ')'
     ;

expr_list = [ expression { ',' expression } ]
          ;

literal = INT_LIT
        | UINT_LIT
        | BOOL_LIT
        | CHAR_LIT
        | DOUBLE_LIT
        ;
