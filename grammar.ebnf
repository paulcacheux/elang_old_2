program = function_def*
        ;

function_def = "fn" IDENTIFIER '(' param_list ')' block
             ;

param_list = [ IDENTIFIER { ',' IDENTIFIER } ]
           ;

block = '{' statement* '}'
      ;

statement = let-statement
          | if-statement
          | loop-statement
          | while-statement
          | break-statement
          | continue-statement
          | return-statement
          | expr-statement
          | block
          ;

if-statement = "if" expression statement [ "else" statement ]
             ;

loop-statement = "loop" statement
               ;

while-statement = "while" expression statement
                ;

break-statement = "break" ";"
                ;

continue-statement = "continue" ";"
                   ;

return-statement = "return" expression ";"
                 ;

let-statement = "let" IDENTIFIER "=" expression ";"
              ;

expr-statement = expression ";"
               ;

expression = IDENTIFIER '=' expression
           | logical_or
           ;

logical_or = logical_and { "||" logical_and }
           ;

logical_and = eq_comp { "&&" eq_comp }
            ;

eq_comp = ord_comp [ (==|!=) ord_comp ]
        ;

ord_comp = sum [ (<|<=|>|>=) sum ]
         ;

sum = factor { (+|-) factor }
    ;

factor = term { (*|/|%) term }
       ;

term = IDENTIFIER
     | IDENTIFIER '(' expr_list ')'
     | NUMBER
     | [+|-|!] term
     | '(' expression ')'
     ;

expr_list = [ expression { ',' expression } ]
          ;
