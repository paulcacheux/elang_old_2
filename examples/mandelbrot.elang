fn print_header(x_max: int, y_max: int) {
    printchar('P');
    println(3);
    print(x_max);
    printchar(' ');
    println(y_max);
    println(255);
}

fn print_rgb(r: int, g: int, b: int) {
    print(r);
    printchar(' ');
    print(g);
    printchar(' ');
    print(b);
    printchar(' ');
}

fn print_iter(iter: int) {
    let x = 6 * iter;
    let r = 0;
    let g = 0;
    let b = 0;
    if x >= 0 && x < 255 {
        r = 255;
        g = x;
        b = 0;
    } else if x >= 255 && x < 510 {
        r = 510 - x;
        g = 255;
        b = 0;
    } else if x >= 510 && x < 765 {
        r = 0;
        g = 255;
        b = x - 510;
    } else if x >= 765 && x < 1020 {
        r = 0;
        g = 1020-x;
        b = 255;
    } else if x >= 1020 && x < 1275 {
        r = x - 1020;
        g = 0;
        b = 255;
    } else if x >= 1275 && x <= 1530 {
        r = 255;
        g = 0;
        b = 1530 - x;
    }
    print_rgb(r, g, b);
}

fn get_iter(px: int, py: int, x_max: double, y_max: double) -> int {
    let xi = (px as double) * 3.5 / x_max - 2.5;
    let yi = (py as double) * 2.0 / y_max - 1.0;

    let x = 0.0;
    let y = 0.0;

    let iter = 0;
    let max_iter = 256;

    while x*x + y*y < 4.0 && iter < max_iter {
        let xtemp = x*x - y*y + xi;
        y = 2.0*x*y + yi;
        x = xtemp;
        iter = iter + 1;
    }

    return iter;
}

fn main() -> ()
{
    let px = 0;
    let py = 0;
    let x_max = 1890.0 * 4.0;
    let y_max = 1080.0 * 4.0;

    print_header(x_max as int, y_max as int);

    while py < y_max as int {
        while px < x_max as int {
            print_iter(get_iter(px, py, x_max, y_max));
            px = px + 1;
        }
        printchar('\n');
        py = py + 1;
        px = 0;
    }
}
